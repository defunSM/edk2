












DefinitionBlock (
  "Tpm.aml",
  "SSDT",
  2,
  "INTEL ",
  "Tpm2Tabl",
  0x1000
  )
{
  Scope (\_SB)
  {
    Device (TPM)
    {







      Name (_HID, "NNNN0000")

      Name (_CID, "MSFT0101")




      Name (_STR, Unicode ("TPM 2.0 Device"))




      OperationRegion (SMIP, SystemIO, 0xB2, 1)
      Field (SMIP, ByteAcc, NoLock, Preserve)
      {
          IOPN, 8
      }




      OperationRegion (TPMR, SystemMemory, 0xfed40000, 0x5000)
      Field (TPMR, AnyAcc, NoLock, Preserve)
      {
        ACC0, 8,
        Offset(0x8),
        INTE, 32,
        INTV, 8,
        Offset(0x10),
        INTS, 32,
        INTF, 32,
        STS0, 32,
        Offset(0x24),
        FIFO, 32,
        Offset(0x30),
        TID0, 32,

      }





      OperationRegion (TNVS, SystemMemory, 0xFFFF0000, 0xF0)
      Field (TNVS, AnyAcc, NoLock, Preserve)
      {
        PPIN, 8,
        PPIP, 32,
        PPRP, 32,
        PPRQ, 32,
        PPRM, 32,
        LPPR, 32,
        FRET, 32,
        MCIN, 8,
        MCIP, 32,
        MORD, 32,
        MRET, 32,
        UCRQ, 32,
        IRQN, 32,
        SFRB, 8
      }









      Name (RESS, ResourceTemplate() {
        Memory32Fixed (ReadWrite, 0xfed40000, 0x5000)
        Interrupt(ResourceConsumer, Level, ActiveLow, Shared, , , ) {1,2,3,4,5,6,7,8,9,10}
      })

      Name (RESL, ResourceTemplate() {
        Memory32Fixed (ReadWrite, 0xfed40000, 0x5000)
        Interrupt(ResourceConsumer, Level, ActiveLow, Shared, , , ) {1,2,3,4,5,6,7,8,9,10,11,12,13,14,15}
      })




      Name(RES0, ResourceTemplate () {
        Memory32Fixed (ReadWrite, 0xfed40000, 0x5000, REG0)
        Interrupt(ResourceConsumer, Level, ActiveLow, Shared, , , INTR) {12}
      })

      Name(RES1, ResourceTemplate () {
        Memory32Fixed (ReadWrite, 0xfed40000, 0x5000, REG1)
      })





      Method(_CRS,0,Serialized)
      {



        If (LEqual(IRQN, 0)) {
          Return (RES1)
        }
        Else
        {
          CreateDWordField(RES0, ^INTR._INT, LIRQ)
          Store(IRQN, LIRQ)
          Return (RES0)
        }
      }









      Method(_SRS,1,Serialized)
      {



        If (LNotEqual(IRQN, 0)) {





          CreateDWordField(Arg0, ^INTR._INT, IRQ0)
          CreateDWordField(RES0, ^INTR._INT, LIRQ)
          Store(IRQ0, LIRQ)
          Store(IRQ0, IRQN)

          CreateBitField(Arg0, ^INTR._HE, ITRG)
          CreateBitField(RES0, ^INTR._HE, LTRG)
          Store(ITRG, LTRG)

          CreateBitField(Arg0, ^INTR._LL, ILVL)
          CreateBitField(RES0, ^INTR._LL, LLVL)
          Store(ILVL, LLVL)







          If (LOr(LEqual (And (TID0, 0x0F), 0x00), LEqual (And (TID0, 0x0F), 0x0F))) {






            If (LLess (IRQ0, 16)) {
              Store (And(IRQ0, 0xF), INTV)
            }















            If (LEqual (ITRG, 1)) {
              Or(INTE, 0x00000010, INTE)
            } Else {
              And(INTE, 0xFFFFFFEF, INTE)
            }
            if (LEqual (ILVL, 0)) {
              Or(INTE, 0x00000008, INTE)
            } Else {
              And(INTE, 0xFFFFFFF7, INTE)
            }
          }
        }
      }

      Method(_PRS,0,Serialized)
      {



        If (LEqual(IRQN, 0)) {
          Return (RES1)
        } ElseIf(LEqual(SFRB, 0)) {



          Return (RESL)
        } Else {



          Return (RESS)
        }
      }

      Method (PTS, 1, Serialized)
      {



        If (LAnd (LLess (Arg0, 6), LGreater (Arg0, 3)))
        {



          If (LNot (And (MORD, 0x10)))
          {



            Store (0x02, MCIP)




            Store (MCIN, IOPN)
          }
        }
        Return (0)
      }

      Method (_STA, 0)
      {
        if (LEqual (ACC0, 0xff))
        {
            Return (0)
        }
        Return (0x0f)
      }




      Method (HINF, 1, Serialized, 0, {BuffObj, PkgObj}, {UnknownObj})
      {



        Switch (ToInteger(Arg0))
        {
          Case (0)
          {



            Return (Buffer () {0x03})
          }
          Case (1)
          {



            Name(TPMV, Package () {0x01, Package () {0x2, 0x0}})
            if (LEqual (_STA (), 0x00))
            {
              Return (Package () {0x00})
            }




            Return (TPMV)
          }
          Default {BreakPoint}
        }
        Return (Buffer () {0})
      }

      Name(TPM2, Package (0x02){
        Zero,
        Zero
      })

      Name(TPM3, Package (0x03){
        Zero,
        Zero,
        Zero
      })




      Method (TPPI, 2, Serialized, 0, {BuffObj, PkgObj, IntObj, StrObj}, {UnknownObj, UnknownObj})
      {



        Switch (ToInteger(Arg0))
        {
          Case (0)
          {



            Return (Buffer () {0xFF, 0x01})
          }
          Case (1)
          {



            Return ("$PV")
          }
          Case (2)
          {




            Store (DerefOf (Index (Arg1, 0x00)), PPRQ)
            Store (0, PPRM)
            Store (0x02, PPIP)




            Store (PPIN, IOPN)
            Return (FRET)


          }
          Case (3)
          {




            Store (PPRQ, Index (TPM2, 0x01))
            Return (TPM2)
          }
          Case (4)
          {



            Return (2)
          }
          Case (5)
          {



            Store (0x05, PPIP)




            Store (PPIN, IOPN)

            Store (LPPR, Index (TPM3, 0x01))
            Store (PPRP, Index (TPM3, 0x02))

            Return (TPM3)
          }
          Case (6)
          {





            Return (3)

          }
          Case (7)
          {



            Store (7, PPIP)
            Store (DerefOf (Index (Arg1, 0x00)), PPRQ)
            Store (0, PPRM)
            If (LEqual (PPRQ, 23)) {
              Store (DerefOf (Index (Arg1, 0x01)), PPRM)
            }




            Store (PPIN, IOPN)
            Return (FRET)
          }
          Case (8)
          {



            Store (8, PPIP)
            Store (DerefOf (Index (Arg1, 0x00)), UCRQ)




            Store (PPIN, IOPN)

            Return (FRET)
          }

          Default {BreakPoint}
        }
        Return (1)
      }

      Method (TMCI, 2, Serialized, 0, IntObj, {UnknownObj, UnknownObj})
      {



        Switch (ToInteger (Arg0))
        {
          Case (0)
          {



            Return (Buffer () {0x03})
          }
          Case (1)
          {



            Store (DerefOf (Index (Arg1, 0x00)), MORD)




            Store (0x01, MCIP)




            Store (MCIN, IOPN)
            Return (MRET)
          }
          Default {BreakPoint}
        }
        Return (1)
      }

      Method (_DSM, 4, Serialized, 0, UnknownObj, {BuffObj, IntObj, IntObj, PkgObj})
      {




        If(LEqual(Arg0, ToUUID ("cf8e16a5-c1e8-4e25-b712-4f54a96702c8")))
        {
          Return (HINF (Arg2))
        }




        If(LEqual(Arg0, ToUUID ("3dddfaa6-361b-4eb4-a424-8d10089d1653")))
        {
          Return (TPPI (Arg2, Arg3))
        }




        If(LEqual(Arg0, ToUUID ("376054ed-cc13-4675-901c-4756d7f2d45d")))
        {
          Return (TMCI (Arg2, Arg3))
        }

        Return (Buffer () {0})
      }
    }
  }
}
