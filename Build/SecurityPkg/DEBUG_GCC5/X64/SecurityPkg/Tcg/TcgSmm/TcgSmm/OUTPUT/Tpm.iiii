











DefinitionBlock (
  "Tpm.aml",
  "SSDT",
  2,
  "INTEL ",
  "TcgTable",
  0x1000
  )
{
  Scope (\_SB)
  {
    Device (TPM)
    {




      Name (_HID, EISAID ("PNP0C31"))




      Name (_STR, Unicode ("TPM 1.2 Device"))




      Name (_CRS, ResourceTemplate () {
        Memory32Fixed (ReadWrite, 0xfed40000, 0x5000)
      })




      OperationRegion (SMIP, SystemIO, 0xB2, 1)
      Field (SMIP, ByteAcc, NoLock, Preserve)
      {
          IOPN, 8
      }




      OperationRegion (TPMR, SystemMemory, 0xfed40000, 0x5000)
      Field (TPMR, AnyAcc, NoLock, Preserve)
      {
        ACC0, 8,
      }





      OperationRegion (TNVS, SystemMemory, 0xFFFF0000, 0xF0)
      Field (TNVS, AnyAcc, NoLock, Preserve)
      {
        PPIN, 8,
        PPIP, 32,
        PPRP, 32,
        PPRQ, 32,
        LPPR, 32,
        FRET, 32,
        MCIN, 8,
        MCIP, 32,
        MORD, 32,
        MRET, 32,
        UCRQ, 32
      }

      Method (PTS, 1, Serialized)
      {



        If (LAnd (LLess (Arg0, 6), LGreater (Arg0, 3)))
        {



          If (LNot (And (MORD, 0x10)))
          {



            Store (0x02, MCIP)




            Store (MCIN, IOPN)
          }
        }
        Return (0)
      }

      Method (_STA, 0)
      {
        if (LEqual (ACC0, 0xff))
        {
            Return (0)
        }
        Return (0x0f)
      }




      Method (HINF, 1, Serialized, 0, {BuffObj, PkgObj}, {UnknownObj})
      {



        Switch (ToInteger(Arg0))
        {
          Case (0)
          {



            Return (Buffer () {0x03})
          }
          Case (1)
          {



            Name(TPMV, Package () {0x01, Package () {0x1, 0x20}})
            if (LEqual (_STA (), 0x00))
            {
              Return (Package () {0x00})
            }




            Return (TPMV)
          }
          Default {BreakPoint}
        }
        Return (Buffer () {0})
      }

      Name(TPM2, Package (0x02){
        Zero,
        Zero
      })

      Name(TPM3, Package (0x03){
        Zero,
        Zero,
        Zero
      })




      Method (TPPI, 2, Serialized, 0, {BuffObj, PkgObj, IntObj, StrObj}, {UnknownObj, UnknownObj})
      {



        Switch (ToInteger(Arg0))
        {
          Case (0)
          {



            Return (Buffer () {0xFF, 0x01})
          }
          Case (1)
          {



            Return ("1.2")
          }
          Case (2)
          {




            Store (DerefOf (Index (Arg1, 0x00)), PPRQ)
            Store (0x02, PPIP)




            Store (PPIN, IOPN)
            Return (FRET)


          }
          Case (3)
          {




            Store (PPRQ, Index (TPM2, 0x01))
            Return (TPM2)
          }
          Case (4)
          {



            Return (2)
          }
          Case (5)
          {



            Store (0x05, PPIP)




            Store (PPIN, IOPN)

            Store (LPPR, Index (TPM3, 0x01))
            Store (PPRP, Index (TPM3, 0x02))

            Return (TPM3)
          }
          Case (6)
          {





            Return (3)

          }
          Case (7)
          {



            Store (7, PPIP)
            Store (DerefOf (Index (Arg1, 0x00)), PPRQ)




            Store (PPIN, IOPN)
            Return (FRET)
          }
          Case (8)
          {



            Store (8, PPIP)
            Store (DerefOf (Index (Arg1, 0x00)), UCRQ)




            Store (PPIN, IOPN)

            Return (FRET)
          }

          Default {BreakPoint}
        }
        Return (1)
      }

      Method (TMCI, 2, Serialized, 0, IntObj, {UnknownObj, UnknownObj})
      {



        Switch (ToInteger (Arg0))
        {
          Case (0)
          {



            Return (Buffer () {0x03})
          }
          Case (1)
          {



            Store (DerefOf (Index (Arg1, 0x00)), MORD)




            Store (0x01, MCIP)




            Store (MCIN, IOPN)
            Return (MRET)
          }
          Default {BreakPoint}
        }
        Return (1)
      }

      Method (_DSM, 4, Serialized, 0, UnknownObj, {BuffObj, IntObj, IntObj, PkgObj})
      {




        If(LEqual(Arg0, ToUUID ("cf8e16a5-c1e8-4e25-b712-4f54a96702c8")))
        {
          Return (HINF (Arg2))
        }




        If(LEqual(Arg0, ToUUID ("3dddfaa6-361b-4eb4-a424-8d10089d1653")))
        {
          Return (TPPI (Arg2, Arg3))
        }




        If(LEqual(Arg0, ToUUID ("376054ed-cc13-4675-901c-4756d7f2d45d")))
        {
          Return (TMCI (Arg2, Arg3))
        }

        Return (Buffer () {0})
      }
    }
  }
}
